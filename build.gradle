plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
}

group = 'com.hexploits.oss.sdk'
version = '0.1.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mock-server:mockserver-netty:5.15.0'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // Ensures the report is generated after tests run
}

jacocoTestReport {
    dependsOn test // Ensures tests run before generating the report

    reports {
        xml.required = true
        html.required = true
    }
}

publishing {
    repositories {
        maven {
            name = "presidio-sdk"
            url = uri("https://maven.pkg.github.com/hexploits/presidio-sdk")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}